## Provided by Dr. Brandon S. Perelman
## U.S. Army Research Laboratory
## 9 MAY 2017

define Start(p)
{


   parpairs <- [["doIntro",1],
                ["doOutro",1],
		["systemName","the system"]] 
   gParams <- CreateParameters(parpairs,gParamFile)

  gDoIntro <- gParams.dointro
  gDoOutro <- gParams.dooutro
  gSystemName <- gParams.systemName

## Load translations
  GetStrings(gLanguage)

  gQuestionHead <- SubstituteStrings(gStrings.question_head,[["<SYSTEM>",gSystemName]])
  InitializeUpload()


  gWin <- MakeWindow("black")

  if(gSubNum+""=="0")
  {
      gSubNum <- GetSubNum(gWin)
  }



  gLikertoptions <- [gStrings.likert_1,
                     gStrings.likert_2,
                     gStrings.likert_3,
                     gStrings.likert_4,
                     gStrings.likert_5]

  MakeDirectory("data/")

   if(gDoIntro)
   {
    intro <- EasyLabel(gStrings.intro,gVideoWidth/2,(gVideoHeight/2)-200,gWin,24)
	Draw()

	WaitForDownClick()

	RemoveObject(intro,gWin)
	Draw()
   }



   gFileOut <- GetNewDataFile(gSubnum,gWin,"SUS","csv","subnum,timestamp,q1,q2,q3,q4,q5,q6,q7,q8,q9,q10,t1,t2,t3,t4,t5,t6,t7,t8,t9,t10")

   dir <- GetDirectory(gFileOut.filename) + "../"
   gPooledFile <-  FileOpenAppend("data/SystemUsability.csv")

  
   ##  Print the question labels
   gQuestfont <- MakeFont(gPEBLBaseFont,0,28,MakeColor("white"),MakeColor("black"),0)
   gSmallFont <- MakeFont(gPEBLBaseFont,0,18,MakeColor("white"),MakeColor("black"),0)
  
  q1 <- gStrings.q1
  q2 <- gStrings.q2
  q3 <- gStrings.q3
  q4 <- gStrings.q4
  q5 <- gStrings.q5
  q6 <- gStrings.q6
  q7 <- gStrings.q7
  q8 <- gStrings.q8
  q9 <- gStrings.q9
  q10 <- gStrings.q10

  
  
  
  
  questions <- [q1,q2,q3,q4,q5,q6,q7,q8,q9,q10]
  
   responsesout <- []
   timesout <- []
   

   ##### Instructions go here if you feel like adding them 
   
   startTime <- GetTime()
  
   
   loop(q,questions)
   {
	t1 <- GetTime()
        resp <- Likert(q)
	t2 <- GetTime()
	PushOnEnd(responsesout, resp)
	PushOnEnd(timesout, t2-t1)
   }
   
   # Print the whole output file for the subject
   line <- gSubNum+","+gQuote+TimeStamp()+gQuote+","+ConcatenateList(Merge(responsesout,timesout),",")
   FilePrint(gFileOut,line)
   FilePrint(gPooledFile,line)
   FileClose(gFileout)
   FileClose(gPooledFile)
   
  UploadFile(gSubnum,gFileout.filename)
  UploadFile(gSubnum,gPooledFile.filename)
	
if(gDoIntro)
   {
    outro <- EasyLabel(gStrings.outro,gVideoWidth/2,(gVideoHeight/2)-200,gWin,24)
	Draw()

	WaitForDownClick()

	RemoveObject(outro,gWin)
	Draw()
   }


}

define GetStrings(lang)
{
  lang <- Uppercase(lang)
  fname <- "translations/SystemUsabilityScale.pbl-" + LowerCase(lang) + ".json"

  if(FileExists(fname))
  {
    gStrings <- ReadTranslationJSON(fname, lang)
  } else {
    gStrings <- ReadTranslationJSON("translations/SystemUsabilityScale.pbl-en.json", lang)
  }
}



##These helper functions require gTextBox, gHeader, and gFooter to work.
define Likert(question)
{
    ##spread should be a total of at least 640 pixels, or 80 per option
    skip <- 150

    ## Add the question heading (this describes the Likert responses)
	
    questionHead <- MakeLabel(gQuestionHead, gQuestFont)
    AddObject(questionHead,gWin)
    Move(questionHead,gVideoWidth/2,150)

	## Add the specific question
	
	question <- MakeLabel(question, gQuestFont)
	AddObject(question, gWin)
	Move(question, gVideoWidth/2, 300)
	
   # question <- EasyTextBox(question,gVideoWidth/2,gVideoHeight/4+75,gWin,24,900,75)
   # AddObject(question,gWin)
   # MoveCenter(question,gVideoWidth/2,300)

    left <- gVideoWidth/2-skip*3
    yval <- gVideoHeight/2+100

    backs <- []
    nums <- []
    texts <- []

    loop(i,[1,2,3,4,5])
     {

      x <- left + skip * i
      back <- Rectangle(x,yval+50,skip-10,180,MakeColor("white"),0)
      num <- MakeLabel(i+"",gQuestFont)
      Move(num, x,yval)
      AddObject(num,gWin)
      AddObject(back,gWin)

     
      optLabel  <- MakeTextBox(Nth(gLikertOptions,i),gSmallFont,skip-15,80) 
      AddObject(optLabel,gWin)
      Move(optlabel,x-optLabel.width/2,yval+50)

      PushOnEnd(backs,back)
      PushOnEnd(nums,num)
      PushOnEnd(texts,optlabel)
       
     }


  Draw()
  #t1 <- GetTime()
  response <- WaitForClickOnTarget(backs,[1,2,3,4,5])
  #t2 <- GetTime()

  return response
}

   



