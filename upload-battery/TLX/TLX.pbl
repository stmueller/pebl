##  This gives the NASA six TLX ratings scales and weight comparisons.
##  pass the task name into the task with the -v TASKNAME command-line option
##
##
##  data are saved to the file tlx-data.csv, with columns:
##  TLX, timestamp, subcode, taskname,W1,W2,W3,W4,W5,W6,R1,R2,R3,R4,R5,R6,RT
##  For dimensions: (mental, physical, temporal, performance,effort,frustration)
##   where:
##      W1..W6 are the assessed weights; which range between 0 and 5 and sum to 15
##      R1..R6 are the ratings, which range between 1 and 21, with 11 being the middle
##    and RT being the number of MS since the task was started.
##
##
define Start(p)
{


  parpairs <- [["doweightings",0],
               ["taskname","Target task"],
	           ["scaleheight",300]
               ]

   gParams <- CreateParameters(parpairs,gParamFile)

   gParams.doweightings <- 1
   ##Should you collect the pairwise weights?
   gDoWeightings <- gParams.doweightings
   gScaleHeight <- gParams.scaleheight

   gWin <- MakeWindow("black")
   gSleepEasy <- 1
   if(gSubNum+"" == "0")
   {
     gSubNum <- GetSubNum(gWin)
   }

   ## Load translations
   GetStrings(gLanguage)

   taskname <- gParams.taskname

   labelmain <- EasyLabel(gStrings.weights_label+taskname,gVideoWidth/2,20,gWin,24)

  scales <- [[gStrings.scale_1_name, gStrings.scale_1_low, gStrings.scale_1_high, gStrings.scale_1_question],
   	 [gStrings.scale_2_name, gStrings.scale_2_low, gStrings.scale_2_high, gStrings.scale_2_question],
	 [gStrings.scale_3_name, gStrings.scale_3_low, gStrings.scale_3_high, gStrings.scale_3_question],
	 [gStrings.scale_4_name, gStrings.scale_4_low, gStrings.scale_4_high, gStrings.scale_4_question],
	 [gStrings.scale_5_name, gStrings.scale_5_low, gStrings.scale_5_high, gStrings.scale_5_question],
	 [gStrings.scale_6_name, gStrings.scale_6_low, gStrings.scale_6_high, gStrings.scale_6_question]]




   dims <- First(Transpose(scales))
   expl <- Nth(Transpose(scales),4)


   inst <- EasyLabel(gStrings.weighting_instruction,gVideoWidth/2,gVideoheight/2-100,gWin,22)
   left <- EasyLabel("",gVideoWidth/2-200,gVideoheight/2,gWin,22)
   right <- EasyLabel("",gVideoWidth/2+200,gVideoheight/2,gWin,22)

   ltext <- EasyTextBox("",gVideoWidth/2-300,gVideoHeight/2+50,gWin,18,200,100)
   rtext <- EasyTextBox("",gVideoWidth/2+100,gVideoHeight/2+50,gWin,18,200,100)

   rectL <- Rectangle(gVideoWidth/2-200,gVideoHeight/2+75,300,200,MakeColor("white"),0)
   rectR <- Rectangle(gVideoWidth/2+200,gVideoHeight/2+75,300,200,MakeColor("white"),0)
   AddObject(rectL,gWin)
   AddObject(rectR,gWin)


   ##there are 6x5/2 = 15 comparisons. We shuffle these, but need to reinstate them into the weightings list
   ##properly.




   weights <- Repeat(0,6)       #data to record. we only need 6
   if(gDoWeightings)
    {
     pairs <- Pairs(Sequence(1,6,1))          #Actual pairings
     order <- Shuffle(Sequence(1,Length(pairs),1))


    ## make all comparisons and count how many winners for each option.
     loop(i,order)
      {

         ##Only do the weight/comparison task if gDoWeightings is 1
         pair <- Nth(pairs,i)  ##select the pair
         use <- Shuffle(pair)  ##randomly order.
	 
	 left.text <- Nth(dims,First(use))
         right.text <- Nth(dims,Second(use))
	 ltext.text <- Nth(expl,First(use))
	 rtext.text <- Nth(expl,Second(use))
	 Draw()
	 resp <- WaitForClickOnTarget([rectL,rectR],use)
	 
	 ##now resp is the preferred dimension. Update the counter.
	 SetElement(weights,resp, Nth(weights,resp)+1)	 




      }
    
    RemoveObject(inst,gWin)
    RemoveObject(left,gWin)
    RemoveObject(right,gWin)
    RemoveObject(ltext,gWin)		 
    RemoveObject(rtext,gWin)		 
    RemoveObject(rectL,gWin)
    RemoveObject(rectR,gWin)

   }

  labelmain.text  <- gStrings.ratings_label+taskname

  xs <- [-300,-180,-60,60,180,300]

  doit<- Transpose(Append(Transpose(scales),xs))


  tmp <- []
  rects <- []
  thumbs <- []
  loop(i,doit)
  {
     out <-  PlotScale(Nth(i,5)+gVideoWidth/2,400,(i) )  

     tmp <- Append(tmp,out)
	 rect <- First(out)
	 rects <- Append(rects,rect)
     line <- Rectangle(Nth(i,5)+gVideoWidth/2,0,31,3,MakeColor("red"),1)
	 thumbs <- Append(thumbs,line)
  }

   Draw()

   rated <- [0,0,0,0,0,0]
   ratings <- [0,0,0,0,0,0]



  ##allow the ratings to be done in any order on the screen, and only allow completion when all are done.
   while (Sum(rated)<6)
    { 
	   click <- WaitForClickOnTarget(rects,Sequence(1,6,1))

	   y <- Second(gClick)
	   
	   rating <- 21- Round(((y-400+gScaleHeight/2)/gScaleHeight)*20) 
	   ylev <- (400 + gScaleHeight/2) - (rating-1) * gScaleHeight/20
       tmpthumb <- Nth(thumbs,click)
       tmpthumb.y <- ylev
       AddObject(tmpthumb,gWin)

	   SetElement(rated,click,1)
	   SetElement(ratings,click,rating) 
       Draw()
    }
    labelmain.text <- gStrings.complete_label
    doneback <- Rectangle(gVideoWidth/2,50,100,30,MakeColor("white"),0)

    AddObject(doneback,gWin)
    done <- EasyLabel(gStrings.done,gVideoWidth/2,50,gWin,18)
    Draw()

   while (click < 7)
    { 
	
	   click <- WaitForClickOnTarget(Append(rects,doneback),Sequence(1,7,1))

	   if(click<7)
       {
 	   y <- Second(gClick)
	   
	   rating <- 21- Round(((y-400+gScaleHeight/2)/gScaleHeight)*20) 
	   ylev <- (400 + gScaleHeight/2) - (rating-1) * gScaleHeight/20
           tmpthumb <- Nth(thumbs,click)
           tmpthumb.y <- ylev
           AddObject(tmpthumb,gWin)

	   SetElement(rated,click,1)
	   SetELement(ratings,click,rating) 

      } 
       Draw()
    }

   Draw()
   outline <- "PEBL-TLX,"+TimeStamp()+","+gSubNum+ ","+taskname
   loop(i,weights)
    {
	  outline <- outline +","+i 
    }

   loop(i,ratings)
    {
	  outline <- outline +","+i 
    }
   outline <- outline + ","+GetTime()



   InitializeUpload()
   fileOut <- GetNewDataFile(gSubnum,gWin,"TLX","csv","TASK,timestamp,subnum,taskname,w1,w2,w3,w4,w5,w6,s1,s2,s3,s4,s5,s6")
   dir <- GetDirectory(fileOut.filename) + "../"
   pooledFileout <-  FileOpenAppend(dir+"TLX-pooled.csv")

   FilePrint(fileout,outline)
   FilePrint(pooledfileout,outline)
   FileClose(fileout)
   FileClose(pooledfileout)
   UploadFile(gSubNum,fileout.filename)
   UploadFile(gSubnum,pooledfileout.filename)

   MessageBox(gStrings.debrief,gWin)
 
  
}



define PlotScale(x,y,info)
{


   black <- MakeColor("black")
   font1 <-   MakeFont(gPEBLBaseFont,1,15,MakeColor("white"),black,0)
   font <-   MakeFont(gPEBLBaseFont,0,12,MakeColor("white"),black,0)
   title <- MakeTextBox(First(info),font1, 110,50)
   AddObject(title,gWin)
   Move(title,x-40,y-gscaleheight/2-170)

   tb <- MakeTextBox(Fourth(info),font, 110,90)
   AddObject(tb,gWin)
   Move(tb,x-40,y-gscaleheight/2-120)
   
   labels <- [Second(info),Third(info)]
   labheights <- [0,21]



   nums <- Sequence(1,21,1)
   lnums <- Length(nums)   

   top <- y-gscaleheight/2
   bottom <- y+gscaleheight/2
   skip <- (bottom-top)/(lnums-1)

   i <- bottom
   ##add click-in box
   box <- Rectangle(x,y,41,gscaleheight+2,MakeColor("grey60"),1)
   AddObject(box,gwin)
   tmp <- [box,title,tb] ##holder of graphical objects       
   loop(n,nums)
   {

     #add tick
	 if(n==11 or n==1 or n==21)
     {
     line <-  Line(x-18,i,37,0,black)
     } else {

     line <-  Line(x-12,i,25,0,black)
     }
     AddObject(line,gwin)
     tmp <- Append(tmp,line)
     i <- i- skip
   }
   


   lab <- MakeLabel(First(labels),font)   
   AddObject(lab,gWin)
   Move(lab,x,bottom+15)
   tmp <- Append(tmp,lab)

   lab <- MakeLabel(Second(labels),font)   
   AddObject(lab,gWin)
   Move(lab,x,top-15)
   tmp <- Append(tmp,lab)


   #add vertical line
   line <-    Line(x,top,0,(bottom-top),black)
   AddObject(line,gwin) 
   Draw()
   tmp <- Append(tmp,line)


  return tmp
}

define Pairs(list)
{
  tmp <- []
  idi <- 1

   loop(i,list)
    {

     idj <- 1
	   loop(j,list)
        {


		  if(idi<idj)
           {

		     tmp <- Append(tmp,[i,j])
           }

		  idj <- idj+1
        }
     idi <- idi + 1
    }

  return tmp
}

define GetStrings(lang)
{
  gStrings <- GetTranslations("TLX", lang)
}
