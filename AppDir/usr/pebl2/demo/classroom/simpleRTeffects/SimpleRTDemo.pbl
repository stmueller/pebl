###########################################################
##  Simple Reaction Time Task, Version 0.2
##  For use with PEBL 0.07 or later
##  http://pebl.sf.net
##  Part of  The PEBL Psychological Testing Battery
##  2006-02 Released into Public Domain
##  by Shane T. Mueller, Ph.D. (smueller at obereed dot net)
##
##
## Simple/Choice Response time demo.
##
define Start(lPar)
{

  ####################################
  ##
  ## Parameters/Variables controlling experiment
  ##
  gResponseKey <- "x"
  gStimulus <- "X"

  
   gWin <- MakeWindow("black")
   gSleepEasy <- 1

   gSubNum <- GetSubNum(gWin)



   MessageBox("You are about to take part in a simple task measuring your reaction time to various stimuli.  First, you should select the conditions you want to test--visual/auditory; constant/variable intensity, and constant/variable time uncertainty.",gWin)


   modality <- GetEasyChoice("Select Modality:",
                ["Visual","auditory"],["v","a"],gWin)

   intensity <- GetEasyChoice("Select Intensity", ["Variable","Constant"],["v","c"], gWin)
   timeuncertainty <- GetEasyChoice("Select temporal uncertainty", ["variable","constant"],["v","c"],gWin)

    ntrials <- 50
    prewarn <- 800
   if(timeuncertainty == "v")
     {
       warningintervals <- SampleEnough([250,750,1250,1750,2250,2750],ntrials)
     } else {
       warningintervals <- SampleEnough([500],ntrials)
    }


##variable or fixed intensity?
  if(intensity == "c")
    {

      vIDs <- Repeat(3,ntrials)
       
     }else{
      ##v
      vIDs <-  SampleEnough([1,2,3,4,5],ntrials)
     }

   if(modality == "v")
   {

    cols <- ["grey10","grey30","grey50","grey70","grey90"]
    vstim <- []	
    loop(i,cols)
      {
        font <- MakeFont(gPEBLBaseFont,0,60,MakeColor(i),MakeColor("black"),0)
	lab <- MakeLabel("X",font)
	AddObject(lab,gWin)
	Move(lab,gVideoWidth/2,gVideoheight/2)
        Hide(lab)
	PushOnEnd(vstim,lab)
      }
   
    }else{


   if(modality == "a")
    {
    vols <-[.1,.2,.3,.5,.9]
    sounds <- []
    loop(i,vols)
     {
 	 Print("Making sonds")
	 tmp <- MakeSineWave(400,500,i)
         PushOnEnd(sounds,tmp)
         PlayForeground(tmp)
     }

     soundvol  <- SampleEnough([1,2,3,4,5],ntrials)
       
     }else{
      
       sounds <- [MakeSineWave(400,500,.5)]
       soundvol <- Repeat(1,ntrials)

     }

  }





  ## These are threshold for rts considered too fast and too slow,
  ## and are not used in the RT stats computations
  toofastThresh <- 100
  tooslowThresh <- 1000
  
  ##Counters that keep track of the number of these responses
  toofast <- 0
  tooslow <- 0
  
  ## Lists storing the actual RT values, along with the presentation time.
  rts    <- []
  intensities <- []
  delays <- []
  delaySucc <- []
  ####################################
  ##
  ## Begin Initialization Procedures.
  ##


    

  ShowCursor(0)

   gFileOut <- GetNewDataFile(gSubNum,gWin,"srt","csv",
    "subNum,block,trial,time0,delay,delay2,rt,resp1,resp2") 
#   PrintProperties(gFileOut)
   
  gInstructions <- EasytextBox("Now that you have selected the options, you will do the task.  You will first do a few practice trials, followed immediately by the test trials.  There will be "+ntrials+" total trials. Whenever you see or hear a stimulus, press the 'x' key as quickly as possible.",50,50,gWin,18,gVideoWidth-100,200)



  gFooter <- EasyLabel("Press 'x' to each stimulus (sound or x)",gVideoWidth/2,gVideoHeight-100,gWin,40)
  #####################################
  ## Begin Experiment with Instructions

  Show(gInstructions)
  Draw()	  
  WaitForAnyKeyPress()
  Hide(gInstructions)
  Draw()

  startstamp <- TimeStamp()

  gWarning <- EasyLabel("+",gVideoWidth/2,gVideoHeight/2,gWin,60)
  Hide(gWarning)
  
  #####################################
  ## Present stimuli
  trial <- 0

  loop(delay, warningintervals)
   {
   trial <- trial + 1
   time0 <- GetTime()

  ##wait the prewarning time:
  Wait(prewarn)
   
  #Draw the plus sign, and wait specified delay
  Show(gWarning)
  Draw()
  Wait(50)
  Hide(gWarning)
  Draw()
  
  
   ##the warning period:  
   resp1 <- WaitForListKeyPressWithTimeout([gResponseKey],delay,1)
   time00 <- GetTime()

  if(resp1 == LowerCase(gresponsekey))
  {  
    ##too fast!
    Wait(delay - (time00-time0))
  } else {  
    resp1 <- "NO_RESPONSE"
  }
  ##Now, the real stimulus!

  if(modality == "v")
  {
   Hide(gWarning)
   intensity <- Nth(vIDS,trial)
   stim <- Nth(vstim,intensity)
   Show(stim)
   Draw()  #stimulus is shown:
   time1 <- GetTime()
   Hide(stim)

  }else{
    ##modality = auditory
    intensity <- Nth(soundvol,trial)
    astim <- Nth(sounds,intensity)

    timestart <- GetTime()
    PlayBackground(astim)
    timereturn <- GetTime()
    time1 <- GetTime()
    Print("Playback delay:" + (timereturn-timestart))
 }
  Hide(gWarning)
  time2 <- GetTime()
  resp2 <- WaitForKeyPress(gresponsekey)
  time3 <- GetTime()
 
  Draw()

  #RT is time3-time1 
  rt <- time3- time1

  FilePrint(gFileOut, gSubNum + "," + trial + "," +  time0 +"," + delay + "," + (time1-time0) + "," + rt  + "," + resp1 + "," + resp2) 


  ####################
  ## Record trial data in local structures so stats can
  ## later be computed.
  ## Only update rts if there wasn't a too-fast error,
  ## and if they were not too slow (greater than 3 seconds)
  if(resp1 == 0 or (time3 - time2) < tooFastThresh)
  {
    toofast <- toofast + 1
  } else {
    if(( rt ) > tooslowThresh)
    {
          tooslow <- tooslow + 1
    } else {
          PushOnEnd(rts, (rt))
	  PushOnEnd(delaySucc,delay)
	  PushOnEnd(intensities,intensity)
    }

   }
 }

  ####################
  ##Experiment is finished; print out summary stats

  reportfile <- FileOpenWrite("data/"+gSubNum+"/srt-report-" + gSubNum + ".txt")

   FilePrint(reportfile,"------------------------------------------------------")
   FilePrint(reportfile," Report for PEBL Simple Response Time Task Version 0.2")
   FilePrint(reportfile, " "+GetPEBLVersion())
   FilePrint(reportfile,"Started at:  " + startstamp)
   FilePrint(reportfile,"Finished at: " +TimeStamp())
   FilePrint(reportfile," http://pebl.sf.net")
   FilePrint(reportfile, " Participant Code: " + gSubNum)
   FilePrint(reportfile,"Stimulus:" + gstimulus)
   FilePrint(reportfile,"------------------------------------------------------")
   FilePrint(reportfile,"Statistic                 Value")
   FilePrint(reportfile,"------------------------------------------------------")

   FilePrint(reportfile,Format("Trials ",28) +ntrials)

   text <- ""
   text <- text +    FilePrint(reportfile,Format("Total Trials",28) +trial)
   text <- text +    FilePrint(reportfile,Format("Anticipations (<" + toofastThresh + ")",28)  + toofast)
   text <- text +    FilePrint(reportfile,Format("Delayed Responses (>"+tooslowThresh+")",28)  + tooSlow)
   text <- text +    FilePrint(reportfile,Format("Correct RT Mean",28) + Mean(rts))
   text <- text +    FilePrint(reportfile,Format("Correct RT Median",28) + Median(rts))
   text <- text +    FilePrint(reportfile,Format("Correct RT Min",28)    + Min(rts))
   text <- text +    FilePrint(reportfile,Format("Correct RT Max",28)    + Max(rts))
   text <- text +    FilePrint(reportfile,Format("Correct RT SD",28)     + StdDev(rts))


  ##aggregate RTs by delay:
   text <- text +       FilePrint(reportfile,"")
   text <- text +       FilePrint(reportfile,"   Delay     N         Mean      Std. Dev")
   text <- text +       FilePrint(reportfile,"   --------------------------------------")
   sumstats <- AggregateRTs(rts,delaySucc)
   loop(stat,sumstats)
   {
   
   text <- text +    FilePrint_(reportfile,"   ")
       loop(s,stat)
       {

         text <- text +    FilePrint_(reportfile,Format(s,10))
       }
         text <- text +     FilePrint(reportfile,"")  ##end-of-line
    }
   FilePrint(reportfile,"------------------------------------------------------")



  ##aggregate RTs by stimulus strengtch
   text2 <-      FilePrint(reportfile,"Response by stimulus strength")
   text2 <- text2 +       FilePrint(reportfile,"   strength     N         Mean      Std. Dev")
   text2 <- text2 +       FilePrint(reportfile,"   --------------------------------------")


  sumstats <- AggregateRTs(rts,intensities)
 
 loop(stat,sumstats)
   {
   
   text2 <- text2 +    FilePrint_(reportfile,"   ")
       loop(s,stat)
       {

         text2 <- text2 +    FilePrint_(reportfile,Format(s,10))
       }
         text2 <- text2 +     FilePrint(reportfile,"")  ##end-of-line
  }
   FilePrint(reportfile,"------------------------------------------------------")


   FileClose(reportfile)
   FileClose(gFileOut)

  ##Now, show debriefing info.
   SetText(gInstructions, text+"  (Press 'X' key to continue)")

    gInstructions.height <- gVideoheight-100
  
   Show(gInstructions)
   Draw()
   WaitForKeyPress("X")

   gInstructions.text <- text2 + CR(1)+"Data report saved in "+reportfile.filename+CR(1)+"Press 'x' to exit."
   Draw()
   WaitForKeyPress("X")

   ShowCursor(1)

}



## This finds mean and SD RT for 
## each of the conditions specified in delays
##
define AggregateRTs(rts, delays)
{

  ## start by sorting both rts and delays by delays;
  ## then move through them and analyze subparts.
  rtX <- SortBy(rts,delays)
  deX <- Sort(delays)
  trials <- Transpose([deX,rtX])
  
  stats <- []

  lastdelay <- First(deX)

  tmpRT <- []  

  loop(i, trials)
  {

     ## if the current delay differs from the previous delay,
	 ## we should analyze what is in tmp right now.
     if(First(i) != lastdelay)
     {

       stats <- Append(stats,
                      [lastdelay, Length(tmpRT),
	                  Mean(tmpRT), StdDev(tmpRT)])
       tmpRT <- []
     }

    lastdelay <- First(i) 
    tmpRT <- Append(tmpRT, Nth(i, 2))
  }
 
 stats <- Append(stats,
                [lastdelay, Length(tmpRT),
                 Mean(tmpRT), StdDev(tmpRT)])

 return stats
}



define SampleEnough(base,n)
{
	
   return Shuffle(SubList(FlattenN(ShuffleRepeat(base,Ceiling(n/Length(base))),1),1,n))
}
