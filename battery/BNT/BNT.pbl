## Based on:
##
##Cokely, E. T., Galesic, M., Ghazal, S., Schulz, E., &
##Garcia-Retamero, R. (2012). Measuring risk literacy:
##The Berlin Numeracy Test. Judgment and Decision
##Making, 7, 25â€“47.


define Start(p)
{

   parpairs <- [["doadaptive",0],
                ["domultiplechoice",0]] 


    gParams <- CreateParameters(parpairs,gParamFile)
    InitializeUpload()  ## Initialize upload system for online testing

    GetQuestions(gLanguage)

    gWin <- MakeWindow("black")
    gSleepEasy <- 1
    if(gSubNum+""=="0")
    {
     gSubNum <- GetSubNum(gWin)
    }
    MakeDirectory("data")

    ## Create subject-specific data file using GetNewDataFile()
    ## This handles Emscripten properly by creating file in /data/{subnum}/
    suboutfile <- GetNewDataFile(gSubNum,gWin,"bnt","csv",
                          "subnum,timestamp,adaptive,mc,totaltime,q1Resp,q1Time,q1Correct,"+
                          "q2Resp,q2Time,q2Correct,"+
                          "q3Resp,q3Time,q3Correct,q4Resp,q4Time,q4Correct,quantile")

    ## Get parent directory for pooled file
    dir <- GetDirectory(suboutfile.filename)+"../"

    ## Create pooled file in parent directory (data/ not data/{subnum}/)
    pooledfname <- dir + "bnt-pooled.csv"
    if(FileExists(pooledfname))
     {
       outfile <- FileOpenAppend(pooledfname)
     }else {
       outfile <- FileOpenWrite(pooledfname)
       FilePrint(outfile,  "subnum,timestamp,adaptive,mc,totaltime,q1Resp,q1Time,q1Correct,"+
                           "q2Resp,q2Time,q2Correct,"+
                           "q3Resp,q3Time,q3Correct,q4Resp,q4Time,q4Correct,quantile")
     }

    ##Make dummy values here:
    out2a <- ["NA","NA","NA"]
    out2b <- ["NA","NA","NA"]
    out3  <- ["NA","NA","NA"]


    ## Show instructions
    MessageBox(gInstructions, gWin)

   if(gparams.doadaptive)
    {
      out1 <- AskQuestion(1,gParams.domultiplechoice)
      
      if(Third(out1)== 0)
       {
          out2a <- AskQuestion(2,gParams.domultiplechoice)
  
          if(Third(out2a)==1)
           {
             quantile <- 2
           }     else {
             quantile <- 1
           }

       }  else {
          out2b <- AskQuestion(3,gParams.domultiplechoice)
          if(Third(out2b)==1)
          {
             quantile <- 4
          } else { 
             out3 <- AskQuestion(4,gParams.domultiplechoice)
             if(Third(out3)==1)
             {
                quantile <- 4
             }else {
                quantile <- 3
             }
          }
       }
  

    } else{

         out1  <- AskQuestion(1,gParams.domultiplechoice)
         out2a <- AskQuestion(2,gParams.domultiplechoice)
         out2b <- AskQuestion(3,gParams.domultiplechoice)
         out3  <- AskQuestion(4,gParams.domultiplechoice)

      quantile <- Third(out1)+Third(out2a)+Third(out2b)+Third(out3)   
    }
 
    data <- ConcatenateList(Flatten([out1,out2a,out2b,out3]),",")
    FilePrint(suboutfile,gSubNum+","+TimeStamp()+ ","+ gParams.doadaptive+","+gParams.domultiplechoice+","+(GetTime()/1000)+ ","+data+","+quantile)
    FilePrint(outfile,gSubNum+","+TimeStamp()+ ","+ gParams.doadaptive+","+gParams.domultiplechoice+","+(GetTime()/1000)+ ","+data+","+quantile)
    FileClose(suboutfile)
    FileClose(outfile)

    ## Upload both subject-specific and pooled data files for online testing
    UploadFile(gSubNum,suboutfile.filename)
    UploadFile(gSubNum,pooledfname)

    MessageBox(gDebriefing +"  ["+ quantile+"]. Click OK to continue.",gWin)
}




define AskQuestion(qnum, doMC)
{

   if(doMC)
    {
      time1 <- GetTime()
      response <- GetEasyChoice(Nth(gQuestions,qNum),Nth(gChoices,qNum),[1,2,3,4],gWin)
      correct <-  response==Nth(gMCCorrect,qNum)
      time2 <- GetTime()

   }else {
     question <- EasyTextBox(Nth(gQuestions,qnum),gVideoWidth/2-600/2,50,gWin,25,600,300)
     back <- Rectangle(gVideoWidth/2,gVideoHeight/2+100,300,50,MakeColor("white"),0)
     anslab <- EasyLabel("",gVideoWidth/2,gVideoHeight/2+100,gWin,30)
     AddObject(back,gWin)

     Draw()   
     time1 <- GetTime()

     response <- GetNumberInput(anslab) 
     correct <- ToNumber(response) == Nth(gAnswers,qnum)
    } 
  time2 <- GetTime()
   
  return [response,(time2-time1),correct]
}


define GetQuestions(lang)
{
  lang <- Uppercase(lang)
  fname <- "translations/BNT.pbl-" + LowerCase(lang) + ".json"

  if(FileExists(fname))
  {
    gStrings <- ReadTranslationJSON(fname, lang)
  } else {
    gStrings <- ReadTranslationJSON("translations/BNT.pbl-en.json", lang)
  }

  ##override answers if necessary for a language
  gAnswers <- [25,30,20,50]

  gInstructions <- gStrings.instructions

  gQuestions <- [gStrings.q1, gStrings.q2, gStrings.q3, gStrings.q4]

  gChoices <- [[gStrings.choice_1a, gStrings.choice_1b, gStrings.choice_1c, gStrings.choice_1d],
               [gStrings.choice_2a, gStrings.choice_2b, gStrings.choice_2c, gStrings.choice_2d],
               [gStrings.choice_3a, gStrings.choice_3b, gStrings.choice_3c, gStrings.choice_3d],
               [gStrings.choice_4a, gStrings.choice_4b, gStrings.choice_4c, gStrings.choice_4d]]

  gMCCorrect <- [2,3,1,3]
  gDebriefing <- gStrings.debriefing
}





define GetNumberInput(label)
{
  opts <- ["1","2","3","4","5","6","7","8","9","0",
           "<kp_1>","<kp_2>","<kp_3>","<kp_4>","<kp_5>","<kp_6>",
           "<kp_7>","<kp_8>","<kp_9>","<kp_0>",
            "<backspace>","<return>" ,"<kp_enter>"]

  done <- 0
  
  out <- ""
  while (not done)
  { 

     resp <- WaitForListKeyPress(opts)
     if(resp == "<return>" or resp == "<kp_enter>")
     {
       done <- 1
     } else {

     if(resp == "<backspace>")
     {
      if(StringLength(out)<=1)
      {
        out <- ""
      } else {
        start <- 1
        max <- Max([1,StringLength(out)-1])
        out <- SubString(out,1,max)
      }
     } else {
   
      out <- out + resp
     }
      
      label.text <- out
      Draw()

     }

  }

 return out

}
