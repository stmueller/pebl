##PEBL Combined Comfort, effort, nd physiological state scales.
## These are all one-dimensional ratings scales, and the results will all be saved in a single file with a common format.


##select any or all, set value to 0 to not test, and another number to include. Order of tests will be determined by the order of the numbers in the parameter sets.
## This asks four questions related to present comfort level,
## hot/cold, wet/dry, and ambient temperature.

define Start(p)
{

  parpairs <- [["docomfort",1],
               ["dohotcold",1],
               ["dowetdry",1],
               ["dotemp",1],
               ["doRPE",1],  ##so-called 'Borg' scale of ratings of perceived exertion
               ["doRSME",1]] ##Rating of subjective mental effort

  gParams <- CreateParameters(parpairs,gParamFile)

  gSleepEasy <- 1
  InitializeUpload()
  gWin <- MakeWindow()

  if(gSubNum+""=="0")
   {
     gSubNum <- GetSubNum(gWin)
   }

  GetStrings(gLanguage)

  tasks <- ["comfort","hotcold","wetdry","temp","RPE","RSME"]
  order <- [gparams.docomfort,gparams.dohotcold,gParams.dowetdry,gParams.dotemp,
           gParams.doRPE,gParams.doRSME]

   ##we need to double-match because I PEBL can't to an exclusion or not here.
  remove <- Match(order,0)
  keep <- Match(remove,0)

 
  tasks <- Filter(tasks,keep)
  order <- Filter(order,keep)

  tasks <- SortBy(tasks,order) ##put these in order of numerical values.

  comfort <- "NA"
  hothold <- "NA"
  wetdry <- "NA"
  temp <- "NA"
  rpe <- "NA"
  rsme <- "NA"



  loop(task,tasks)
  {
    if(task=="comfort")
    {
       comfort  <-  Comfort(gWin)
    }elseif(task=="hotcold")
    {
      hotcold <-  HotCold(gWin)
    } elseif(task=="wetdry")
    {
     wetdry <- WetDry(gWin)
    }elseif(task=="temp")
   {
    temp     <- ( TempChange(gWin))
   }elseif(task=="RPE")
   {
     rpe <- BorgRPE(gWin)
   }elseif(task == "RSME")
   {
     rsme <- RSME(gWin)
   } 
    
   }

  gFileOut <- GetNewDataFile(gSubNum,gWin,"physioscales","csv","subnum,timestamp,time,comfort,hotcold,wetdry,temp,borgrpe,rsme")
  FilePrint(gfileout, gSubNum + "," + TimeStamp() +","+(GetTime()/1000)+","+ comfort + "," + hotcold + ","+ wetdry + "," + temp+","+ rpe+  "," + rsme)
  FileClose(gFileOut)
  UploadFile(gSubNum,gFileOut.filename)
}



define Comfort(win)
{
   testwidth <-  Min([gVideoWidth-50,900])
   black <- MakeColor("black")

   font <-   MakeFont(gPEBLBaseFont,0,18,MakeColor("grey90"),black,1)

   label <- MakeTextBox(gStrings.comfort_label,font ,testwidth,50)
   AddObject(label,win)
   Move(label, gVideoWidth/2-testwidth/2,20)

   labels <- [gStrings.comfort_label_1, gStrings.comfort_label_2,
    gStrings.comfort_label_3, gStrings.comfort_label_4, gStrings.comfort_label_5,
	gStrings.comfort_label_6, gStrings.comfort_label_7,
    gStrings.comfort_label_8, gStrings.comfort_label_9, gStrings.comfort_label_10,
    gStrings.comfort_label_11]
   labheights <- [-100,-84,-61,-42,-18,0,8,37,56,70,100]


   xline <- gVideoWidth/3  ##1/3 of the way across the screen
   
   nums <- Sequence(-100,100,20)
   lnums <- Length(nums)   
   top <- 100

   ##make the bottom no more than 800 tall 
   bottom <- Min([gVideoHeight-50,800])

   skip <- (bottom-top)/(lnums-1)
   i <- bottom
   ##add click-in box
    box <- Rectangle(xline,(top+bottom)/2,25,(bottom-top)+2,MakeColor("grey70"),1)
    AddObject(box,win)
   tmp <- [label,box] ##holder of graphical objects       
   loop(n,nums)
   {
     lab <- MakeLabel(n+"",font)
     AddObject(lab,win)
     Move(lab,xline-20-lab.width/2,i)

     tmp <- Append(tmp,lab)     

     #add tick
     line <-  Line(xline-10,i,9,0,black)
     AddObject(line,win)
     tmp <- Append(tmp,line)

     i <- i- skip
   }
    
   ##Add text labels
   loop(i,Transpose([labels,labheights]))
   {
      lab <- MakeLabel(First(i),font)
      AddObject(lab,win)
      y <-  bottom - (Second(i)+100)/201 * (bottom-top)
      Move(lab,xline+15 + lab.width/2,y)
      tmp <- Append(tmp,lab)
   #add tick
     line <-  Line(xline,y,9,0,black)
     AddObject(line,win)
     tmp <- Append(tmp,line)

   }

   #add vertical line
   line <-    Line(xline,top,0,(bottom-top),black)
   AddObject(line,win) 
   Draw()
   tmp <- Append(tmp,line)

   click <- WaitForClickOnTarget([box],[1])
   line <- Line(xline-10,Second(gClick),20,0,MakeColor("red"))
   AddObject(line,win)

   Draw()

  ## Make a 'continue' button

  back <- Rectangle(xline+100,bottom+30,140,30,MakeColor("grey60"),0)
  AddObject(back,win)
  cont <- MakeLabel(gStrings.continue,font)
  AddObject(cont,win)
  Move(cont,xline+100,bottom + 30)
  Draw()
  continue <- 1
  while(continue)
   {
     resp <- WaitForClickOnTarget([back,box],[0,1])
     if(resp == 1 )
     {
       line.y <- Second(gClick)
       Draw()
     } else {

      continue <- 0
     }


   }

  RemoveObjects(tmp,win)
  RemoveObject(back,win)
  RemoveObject(cont,win)
  RemoveObject(line,win)


  return (bottom-line.y)/(bottom-top)*200-100
}




define WetDry(win)
{


   testwidth <-  Min([gVideoWidth-50,900])
   label <- EasyTextBox(gStrings.wetdry_label ,gVideoWidth/2-testwidth/2,20,win,16,testwidth,50)
   black <- MakeColor("black")
   font <-   MakeFont(gPEBLBaseFont,0,18,MakeColor("grey90"),black,0)
   labels <- [gStrings.wetdry_label_1, gStrings.wetdry_label_2, gStrings.wetdry_label_3]
   labheights <- [0,3,6]

   xline <- gVideoWidth/2

   nums <- Sequence(0,6,1)
   lnums <- Length(nums)   
   top <- 100

   ##make the bottom no more than 800 tall 
   bottom <- Min([gVideoHeight-50,800])

   skip <- (bottom-top)/(lnums-1)
   i <- bottom
   ##add click-in box
    box <- Rectangle(xline,(top+bottom)/2,25,(bottom-top)+2,MakeColor("grey70"),1)
    AddObject(box,win)
   tmp <- [label,box] ##holder of graphical objects       
   loop(n,nums)
   {


     lab <- MakeLabel(""+n,font)
     AddObject(lab,win)
     Move(lab,xline+20+lab.width/2,i)

     tmp <- Append(tmp,lab)     

     #add tick
     line <-  Line(xline,i,9,0,black)
     AddObject(line,win)
     tmp <- Append(tmp,line)

     i <- i- skip
   }
   
   ##add the text labels
   loop(i,Transpose([labels,labheights]))
   {
      lab <- MakeLabel(First(i),font)
      AddObject(lab,win)
      y <-  bottom - (Second(i))/6 * (bottom-top)
      Move(lab,xline-15 - lab.width/2,y)
      tmp <- Append(tmp,lab)
   }

   #add vertical line
   line <-    Line(xline,top,0,(bottom-top),black)
   AddObject(line,win) 
   Draw()
   tmp <- Append(tmp,line)

   click <- WaitForClickOnTarget([box],[1])
   line <- Line(xline-10,Second(gClick),20,0,MakeColor("red"))
   AddObject(line,win)

   Draw()

  ## Make a 'continue' button

  cont <- MakeLabel(gStrings.continue,font)
  AddObject(cont,win)
  Move(cont,xline,bottom + 30)
  back <- Rectangle(xline,bottom+30,cont.width+6,30,MakeColor("grey70"),0)
  AddObject(back,win)

  Draw()
  continue <- 1
  while(continue)
   {
     resp <- WaitForClickOnTarget([back,box],[0,1])
     if(resp == 1 )
     {
       line.y <- Second(gClick)
       Draw()
     } else {

      continue <- 0
     }


   }

  RemoveObjects(tmp,win)
  RemoveObject(back,win)
  RemoveObject(cont,win)
  RemoveObject(line,win)


  return (bottom-line.y)/(bottom-top)*6
}



define HotCold(win)
{

   testwidth <-  Min([gVideoWidth-50,900])
   label <- EasyTextBox(gStrings.hotcold_label ,gVideoWidth/2-testwidth/2,20,win,16,testwidth,50)
   black <- MakeColor("black")
   font <-   MakeFont(gPEBLBaseFont,0,18,MakeColor("grey90"),black,0)
   labels <- [gStrings.hotcold_label_1, gStrings.hotcold_label_2, gStrings.hotcold_label_3,
              gStrings.hotcold_label_4, gStrings.hotcold_label_5, gStrings.hotcold_label_6,
              gStrings.hotcold_label_7, gStrings.hotcold_label_8, gStrings.hotcold_label_9]
   labheights <- Sequence(-4,4,1)


   xline <- gVideoWidth/2

   nums <- Sequence(-4,4,1)
   lnums <- Length(nums)   
   top <- 100

   ##make the bottom no more than 800 tall 
   bottom <- Min([gVideoHeight-50,800])

   skip <- (bottom-top)/(lnums-1)
   i <- bottom
   ##add click-in box
    box <- Rectangle(xline,(top+bottom)/2,25,(bottom-top)+2,MakeColor("grey70"),1)
    AddObject(box,win)
   tmp <- [label,box] ##holder of graphical objects       
   loop(n,nums)
   {

     if(n>0) {
         sign <- "+"
     }else{
         sign <- ""
     }

     lab <- MakeLabel(sign+n,font)
     AddObject(lab,win)
     Move(lab,xline+20+lab.width/2,i)

     tmp <- Append(tmp,lab)     

     #add tick
     line <-  Line(xline,i,9,0,black)
     AddObject(line,win)
     tmp <- Append(tmp,line)

     i <- i- skip
   }
   
   loop(i,Transpose([labels,labheights]))
   {
      lab <- MakeLabel(First(i),font)
      AddObject(lab,win)
      y <-  bottom - (Second(i)+4)/8 * (bottom-top)
      Move(lab,xline-15 - lab.width/2,y)
      tmp <- Append(tmp,lab)

   }

   #add vertical line
   line <-    Line(xline,top,0,(bottom-top),black)
   AddObject(line,win) 
   Draw()
   tmp <- Append(tmp,line)

   click <- WaitForClickOnTarget([box],[1])
   line <- Line(xline-10,Second(gClick),20,0,MakeColor("red"))
   AddObject(line,win)

   Draw()

  ## Make a 'continue' button

  back <- Rectangle(xline,bottom+30,140,30,MakeColor("grey70"),0)
  AddObject(back,win)
  cont <- MakeLabel(gStrings.continue,font)
  AddObject(cont,win)
  Move(cont,xline,bottom + 30)
  Draw()
  continue <- 1
  while(continue)
   {
     resp <- WaitForClickOnTarget([back,box],[0,1])
     if(resp == 1 )
     {
       line.y <- Second(gClick)
       Draw()
     } else {

      continue <- 0
     }


   }

  RemoveObjects(tmp,win)
  RemoveObject(back,win)
  RemoveObject(cont,win)
  RemoveObject(line,win)


  return (bottom-line.y)/(bottom-top)*8-4
}




define TempChange(win)
{
   testwidth <-  Min([gVideoWidth-50,900])
   label <- EasyTextBox(gStrings.tempchange_label ,gVideoWidth/2-testwidth/2,20,win,16,testwidth,50)
   black <- MakeColor("black")
   font <-   MakeFont(gPEBLBaseFont,0,18,MakeColor("grey90"),black,0)
   labels <- [gStrings.tempchange_label_1, gStrings.tempchange_label_2, gStrings.tempchange_label_3]
   labheights <- [0,3,6]

   xline <- gVideoWidth/2

   nums <- Sequence(0,6,1)
   lnums <- Length(nums)   
   top <- 100
   ##make the bottom no more than 800 tall 
   bottom <- Min([gVideoHeight-50,800])

   skip <- (bottom-top)/(lnums-1)
   i <- bottom
   ##add click-in box
    box <- Rectangle(xline,(top+bottom)/2,25,(bottom-top)+2,MakeColor("grey70"),1)
    AddObject(box,win)
   tmp <- [label,box] ##holder of graphical objects       
   loop(n,nums)
   {


     lab <- MakeLabel(""+n,font)
     AddObject(lab,win)
     Move(lab,xline+20+lab.width/2,i)

     tmp <- Append(tmp,lab)     

     #add tick
     line <-  Line(xline,i,9,0,black)
     AddObject(line,win)
     tmp <- Append(tmp,line)

     i <- i- skip
   }
   
   ##add the text labels
   loop(i,Transpose([labels,labheights]))
   {
      lab <- MakeLabel(First(i),font)
      AddObject(lab,win)
      y <-  bottom - (Second(i))/6 * (bottom-top)
      Move(lab,xline-15 - lab.width/2,y)
      tmp <- Append(tmp,lab)
   }

   #add vertical line
   line <-    Line(xline,top,0,(bottom-top),black)
   AddObject(line,win) 
   Draw()
   tmp <- Append(tmp,line)

   click <- WaitForClickOnTarget([box],[1])
   line <- Line(xline-10,Second(gClick),20,0,MakeColor("red"))
   AddObject(line,win)

   Draw()

  ## Make a 'continue' button

  cont <- MakeLabel(gStrings.continue,font)
  back <- Rectangle(xline,bottom+30,cont.width+6,30,MakeColor("grey60"),0)
  AddObject(back,win)

  AddObject(cont,win)
  Move(cont,xline,bottom + 30)
  Draw()
  continue <- 1
  while(continue)
   {
     resp <- WaitForClickOnTarget([back,box],[0,1])
     if(resp == 1 )
     {
       line.y <- Second(gClick)
       Draw()
     } else {

      continue <- 0
     }

      
   }

  RemoveObjects(tmp,win)
  RemoveObject(back,win)
  RemoveObject(cont,win)
  RemoveObject(line,win)


  return (bottom-line.y)/(bottom-top)*6
}




define RSME(win)
{
   testwidth <-  Min([gVideoWidth-50,900])
   black <- MakeColor("black")
   font <-   MakeFont(gPEBLBaseFont,0,18,MakeColor("grey90"),black,0)

   
   label <- MakeTextBox(gStrings.rsme_label,font, testwidth,80)
   AddObject(label,win)
   Move(label,gVideoWidth/2-testwidth/2,20)

   labels <- [gStrings.rsme_label_9, gStrings.rsme_label_8, gStrings.rsme_label_7,
              gStrings.rsme_label_6, gStrings.rsme_label_5, gStrings.rsme_label_4,
              gStrings.rsme_label_3, gStrings.rsme_label_2, gStrings.rsme_label_1]
   labheights <- [113, 102, 85,72,57,38,26,13,3]

   xline <- gVideoWidth/2
   scaleMax <- 150
   scaleMin <- 0
   scaleSize <- scaleMax-scaleMin

   nums <- Sequence(scaleMin,scaleSize,10)
   lnums <- Length(nums)   
   ##bottom and top in pixels:
   top <- 150
   ##make the bottom no more than 800 tall 
   bottom <- Min([gVideoHeight-50,800])

   scale <- (bottom-top)/scaleSize
   skip <- (bottom-top)/(lnums-1)
   i <- bottom

   ##add click-in box
    box <- Rectangle(xline,(top+bottom)/2,25,(bottom-top)+2,MakeColor("grey60"),1)
    AddObject(box,win)
   tmp <- [label,box] ##holder of graphical objects       


   loop(n,nums)
   {
     lab <- MakeLabel(n+"",font)
     AddObject(lab,win)
     Move(lab,xline-20-lab.width/2,i)

     tmp <- Append(tmp,lab)     

     #add tick
     line <-  Line(xline-10,i,9,0,black)
     AddObject(line,win)
     tmp <- Append(tmp,line)

     i <- i- skip
   }
    

   ##Add text labels
   loop(i,Transpose([labels,labheights]))
   {
      lab <- MakeLabel(First(i),font)
      AddObject(lab,win)
      y <-  bottom - (Second(i)) * scale
      Move(lab,xline+15 + lab.width/2,y)
      tmp <- Append(tmp,lab)
      #add tick
     line <-  Line(xline,y,9,0,black)
     AddObject(line,win)
     tmp <- Append(tmp,line)

   }

   #add vertical line
   line <-    Line(xline,top,0,(bottom-top),black)
   AddObject(line,win) 
   Draw()
   tmp <- Append(tmp,line)

   click <- WaitForClickOnTarget([box],[1])
   line <- Line(xline-10,Second(gClick),20,0,MakeColor("red"))
   AddObject(line,win)

   Draw()

  ## Make a 'continue' button

  cont <- MakeLabel(gStrings.continue,font)
  AddObject(cont,win)
  Move(cont,xline+100,bottom + 30)
  back <- Rectangle(xline+100,bottom+30,cont.width+6,30,MakeColor("grey60"),0)
  AddObject(back,win)

  Draw()
  continue <- 1
  while(continue)
   {
     resp <- WaitForClickOnTarget([back,box],[0,1])
     if(resp == 1 )
     {
       line.y <- Second(gClick)
       Draw()
     } else {

      continue <- 0
     }

      
   }

  RemoveObjects(tmp,win)
  RemoveObject(back,win)
  RemoveObject(cont,win)
  RemoveObject(line,win)


  return scaleMin + (bottom-line.y)/(bottom-top)*scaleSize
}









define MakeCSVLine(list)
{
  sep <- ""
  out <- ""
  loop(i,list)
  {
    out <- out + sep + i
    sep <- ","
  }
 return out
}





define BorgRPE(win)
{
   testwidth <-  Min([gVideoWidth-50,900])
   black <- MakeColor("black")
   font <-   MakeFont(gPEBLBaseFont,0,17,MakeColor("white"),MakeColor("black"),0)

   label <- MakeTextBox(gStrings.BORG_inst1,font , testwidth,300)
   AddObject(label,win)
   Move(label,gVideoWidth/2-testwidth/2,10)
   
   labels <- [gStrings.BORG_LABEL_1,gStrings.BORG_LABEL_2,gStrings.BORG_LABEL_3,
              gStrings.BORG_LABEL_4,gStrings.BORG_LABEL_5,gStrings.BORG_LABEL_6,
              gStrings.BORG_LABEL_7,gStrings.BORG_LABEL_8,gStrings.BORG_LABEL_9]
          
   labheights <- [6,7.5,9,11,13,15,17,19,20]


  explanations <-[gStrings.BORG_EXPL_1,gStrings.BORG_EXPL_2,gStrings.BORG_EXPL_3,
              gStrings.BORG_EXPL_4,gStrings.BORG_EXPL_5,gStrings.BORG_EXPL_6]
   expheights <- [7,9,11.5,15,17.5,19.5]


 

   scaleMax <- 20
   scaleMin <- 6
   scaleSize <- scaleMax-scaleMin


   nums <- Sequence(scaleMin,scaleMax,1)
   lnums <- Length(nums)   
   ##bottom and top in pixels:
   top <- 150
   ##make the bottom no more than 800 tall 
   bottom <- Min([gVideoHeight-50,800])

   scale <- (bottom-top)/scaleSize
   skip <- (bottom-top)/(lnums-1)



   ##the
   range <- bottom-top ##how many pixels do yo ureally have?
   ycenter1 <- top + 160/750*range
   ycenter2 <- top + 435/750*range
   ycenter3 <- top + 650/750*range
   yheight1 <- (320/750)*range
   yheight2 <- (230/750)*range
   yheight3 <- (200/750)*range
   ##these boxes must be scaled to the items they represent.
  box1 <- Rectangle(gVideoWidth/2 + testWidth*.14,ycenter1,testwidth*.64,yheight1,MakeColor("cadetblue4"),1)
  box2 <- Rectangle(gVideoWidth/2 + testWidth*.14,ycenter2,testWidth*.64,yheight2,MakeColor("green4"),1)
  box3 <- Rectangle(gVideoWidth/2 + testWidth*.14,ycenter3,testWidth*.64,yheight3,MakeColor("darkred"),1)

  AddObject(box1,gWin)
  AddObject(box2,gWin)
  AddObject(box3,gWin)



  i <- top
  ##xline for the box.
  lineX <- gVideoWidth/2-testwidth/2+50

   ##add click-in box25 pixels wide.
    box <- Rectangle(lineX,(top+bottom)/2,26,(bottom-top)+2,MakeColor("grey60"),1)
    AddObject(box,win)
   tmp <- [label,box] ##holder of graphical objects       

  ##add numbers:
   loop(n,nums)
   {

  
     lab <- MakeLabel(n+"",font)
     AddObject(lab,win)
     Move(lab,linex-lab.width/2-box.width/2-2,i)

     tmp <- Append(tmp,lab)     

     #add tick
     line <-  Line(Floor(lineX-box.width/2),i,Floor(box.width/2),0,black)
     AddObject(line,win)
     tmp <- Append(tmp,line)

     i <- i+ skip
   }
    

   ##Add text labels
   loop(i,Transpose([labels,labheights]))
    {
      lab <- MakeLabel(First(i),font)
      AddObject(lab,win)
      y <-  top + (Second(i)-scaleMin) * scale
      Move(lab,lineX + 15 + lab.width/2,y)
      tmp <- Append(tmp,lab)
      #add tick
     line <-  Line(Floor(lineX),y,Floor(box.width/2),0,black)
     AddObject(line,win)
     tmp <- Append(tmp,line)

   }

##Add text explanations
##colored boxes start 1/3 of the screen
   loop(i,Transpose([explanations,expheights]))
   {
      lab <- MakeLabel(First(i),font)
      AddObject(lab,win)
      y <-  top + (Second(i)-scaleMin) * scale
      x <- gVideoWidth/2 -testwidth/6 + lab.width/2 + 5
      Move(lab,x,y)
      tmp <- Append(tmp,lab)
      tmp <- Append(tmp,line)

   }

   #add vertical line
   line <-    Line(lineX,top,0,(bottom-top),black)
   AddObject(line,win) 
   Draw()
   tmp <- Append(tmp,line)

   click <- WaitForClickOnTarget([box],[1])
   selectedvalue <- Round(scaleMin + scaleSize * (Second(gClick) - top)/(bottom-top))
   roundedheight <- (selectedValue-scalemin) * skip + top

  
   line <-  ThickLine(lineX-box.width/2,roundedheight,lineX+box.width/2,roundedheight,3,MakeColor("red"))
   AddObject(line,win)

   Draw()

  ## Make a 'continue' button

  cont <- MakeLabel(gStrings.continue,font)
  Move(cont,lineX+100,bottom + 30)

  back <- Rectangle(cont.x,cont.y,cont.width+6,30,MakeColor("grey60"),0)
  AddObject(cont,win)
  AddObject(back,win)

  Draw()
  continue <- 1
  while(continue)
   {
     resp <- WaitForClickOnTarget([back,box],[0,1])


     if(resp == 1 )  ##Hit box; need to calculate and place the line now.
     {


       selectedvalue <- Round(scaleMin + scaleSize * (Second(gClick) - top)/(bottom-top))


       roundedheight <- (selectedValue-scalemin) * skip + top

       line.y <- roundedheight
       
       Draw()
       
     } else {  ##hit 'ok'

      continue <- 0
     }

      
   }


  return selectedvalue
}





define GetStrings(lang)
{
  gStrings <- GetTranslations("VAScales", lang)
}

