define Start(p)
{
   gWin <- MakeWindow("black")

   parPairs <- [["reps",10],
                ["isi",500],
                ["minval",100],
                ["maxval",999],
                ["responsemode","auto"],
                ["responselabels",["ODD","EVEN"]]]  ##Semantic labels for response modes


   gParams <- CreateParameters(parpairs,gParamFile)


   GetStrings(gLanguage)

   ## Set semantic labels from translation strings
   gParams.responselabels <- [gStrings.odd, gStrings.even]

   if(gSubNum+""=="0")
    {
      gSubNum <- GetSubNum(gWin)
    }

    header <- "subnum,trial,starttime,number,isodd,resp,corr,rt"
    gFileOut <- GetNewDataFile(gSubNum,gWin,"evenodd","csv",header)

   ## Create layout with response system
   gLayout <- CreateLayout("evenodd", gWin, gParams)

   ## Configure layout zones
   gLayout.header.text <- gStrings.header
   gLayout.subheader.visible <- 0
   gLayout.footer.text <- gStrings.footer

   ## Hide cursor for keyboard and mousebutton modes
   ## Show cursor only for mousetarget mode (not touchtarget - no cursor on touch screens)
   if(gLayout.responseMode.type == "mousetarget")
   {
     ShowCursor(1)
   } else {
     ShowCursor(0)
   }

   Draw()

  ShowInstructions(gStrings.inst1)

  if(IsOdd(gParams.minval))
  {
    minOdd <- gParams.minval
    minEven <- gParams.minval + 1
  } else{
    minOdd <- gParams.minval+1
    minEven <- gParams.minval
  }

   ##Create the stimulus sequence we will use:
   desOdd <- Sequence(minOdd,gParams.maxval,2)
   desEven <- Sequence(minEven,gParams.maxval,2)

   trialsOdd <- SampleN(desOdd,gParams.reps)
   trialsEven <- SampleN(desEven,gParams.reps)



    trials <- Shuffle(Merge(trialsOdd,trialsEven))


     ##do some practice trials that aren't recorded:
	 Trial(1)
	 Wait(gParams.ISI)

	 Trial(24)
	 Wait(gParams.ISI)

	 Trial(97)
	 Wait(gParams.ISI)

    rts <- []
	corrs <- []

   trial <- 1
   loop(i,trials)
   {
     starttime <- GetTime()
     out <-  Trial(i)
     FilePrintList(gFileout,Merge([gSubNum,trial,starttime],out),",")
     PushOnEnd(rts,Nth(out,5))
     PushOnEnd(corrs,Nth(out,4))

     trial <- trial + 1

      Wait(gParams.isi)
   }


    meanRT <- Mean(rts)
	medianRT <- Median(rts)
	meanAcc <- Mean(corrs)

	report <- "PEBL Even-Odd Test"+CR(1)+
	"Participant:     "+ gSubNum + CR(1)+
	TimeStamp() + CR(1)+
	"Number of trials: " + Length(corrs) + CR(1)+
	"Mean RT:          "+ meanRT + CR(1)+
	"Median RT:        " + medianRT + CR(1)+
	"Accuracy:         " + (meanACC * 100) + "%" + CR(1)

	reportfile <- GetNewDataFile(gSubNum,gWin,"evenodd-report","txt","")
	FilePrint(reportfile,report)

   ShowInstructions(gStrings.debrief+ CR(2)+ report)
}


define ShowInstructions(message)
{
  ## Display instructions within stimulus region with response labels visible
  instWidth <- 750
  instX <- gVideoWidth/2 - instWidth/2
  instY <- gLayout.stimulusRegion.y + 20
  instHeight <- gLayout.stimulusRegion.height - 40

  ## Create adaptive textbox in instruction area
  inst <- AdaptiveTextBox(message,
                          instX,
                          instY,
                          gWin, 32,
                          instWidth,
                          instHeight,
                          "scalefont")

  Draw()

  ## Wait for any response compatible with current mode
  if(gLayout.responseMode.type == "keyboard" or
     gLayout.responseMode.type == "keyboardShift" or
     gLayout.responseMode.type == "keyboardSafe" or
     gLayout.responseMode.type == "singlekey")
  {
    WaitForAnyKeyPress()
  } else {
    ## Mouse/touch modes - use WaitForDownClick to avoid upclick passthrough
    WaitForDownClick()
  }

  RemoveObject(inst, gWin)
}


define Trial(number)
{
  ## Header and footer are already set in layout
  ## Just create the stimulus in the center of the stimulus region
  stim <- EasyLabel(number+"", gLayout.centerX, gLayout.centerY, gWin, 80)

  Draw()
  time1 <- GetTime()

  ## Use layout response system (returns "left" or "right")
  resp <- WaitForLayoutResponse(gLayout, 5000)

  time2 <- GetTime()
  rt <- (time2-time1)
  odd <- IsOdd(number)

  ## Semantic response mapping: left=odd, right=even
  corr <- (odd and resp=="left") or ((not odd) and resp=="right")

  Hide(stim)
  Draw()

  return [number, odd, resp, corr, rt]
}

define IsOdd(number)
{
    return Mod(number,2)
}


define GetStrings(lang)
{
  gStrings <- GetTranslations("evenodd", lang)
}
